plugins {
	id 'com.android.library'
	id 'maven-publish'
}

android {
	namespace 'ovh.karewan.knhttp'
	compileSdk = 36

	defaultConfig {
		minSdk 21
		targetSdk 36
		versionCode 40
		versionName "3.1.4"
		consumerProguardFiles 'proguard-rules.pro'
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	lintOptions {
		abortOnError false
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_21
		targetCompatibility JavaVersion.VERSION_21
	}

	publishing {
		singleVariant("release")
	}
}

dependencies {
	implementation(platform("org.jetbrains.kotlin:kotlin-bom:2.2.10"))
	implementation 'androidx.annotation:annotation:1.9.1'
	implementation 'org.conscrypt:conscrypt-android:2.5.3'
	api 'com.squareup.okio:okio:3.16.0'
	api ('com.squareup.okhttp3:okhttp:5.1.0') {
		exclude group: 'com.squareup.okio', module:'okio'
		exclude group: 'com.squareup.okio', module:'okio-jvm'
	}
	implementation 'com.squareup.okhttp3:okhttp-brotli:5.1.0'
	api 'com.alibaba:fastjson:1.1.76.android'
}

afterEvaluate {
	publishing {
		publications {
			release(MavenPublication) {
				from components.release
				groupId = 'com.github.Karewan'
				artifactId = 'KnHttp'
				version = android.defaultConfig.versionName
			}
		}
	}
}
